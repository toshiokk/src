#	半角文字を全角文字に

/[!-ﾟ]/{

# 先行する文字が半角片仮名であれば "ｰ"（音引き）を”ー”に
s/\([ｦ-ﾟ]\)ｰ/\1ー/g

# そうでない "ｰ"（音引き）は ”−”に
s/ｰ/−/g

# 半角ｶﾅを全角カナへ

y/ｧｱｨｲｩｳｪｴｫｵｶｷｸｹｺｻｼｽｾｿﾀﾁｯﾂﾃﾄﾅﾆﾇﾈﾉﾊﾋﾌﾍﾎﾏﾐﾑﾒﾓｬﾔｭﾕｮﾖﾗﾘﾙﾚﾛﾜｦﾝﾞﾟ/ァアィイゥウェエォオカキクケコサシスセソタチッツテトナニヌネノハヒフヘホマミムメモャヤュユョヨラリルレロワヲン゛゜/

s/カ゛/ガ/g
s/キ゛/ギ/g
s/ク゛/グ/g
s/ケ゛/ゲ/g
s/コ゛/ゴ/g
s/サ゛/ザ/g
s/シ゛/ジ/g
s/ス゛/ズ/g
s/セ゛/ゼ/g
s/ソ゛/ゾ/g
s/タ゛/ダ/g
s/チ゛/ヂ/g
s/ツ゛/ヅ/g
s/テ゛/デ/g
s/ト゛/ド/g
s/ハ゛/バ/g
s/ヒ゛/ビ/g
s/フ゛/ブ/g
s/ヘ゛/ベ/g
s/ホ゛/ボ/g
s/ハ゜/パ/g
s/ヒ゜/ピ/g
s/フ゜/プ/g
s/ヘ゜/ペ/g
s/ホ゜/ポ/g
s/ウ゛/ヴ/g

# 句読点と括弧

y/｡､｢｣ｰ/。、「」ー/

# 他の文字種は別ファイルの方が良かったかな？

y/0123456789+-$%#&*@=ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz/０１２３４５６７８９＋−＄％＃＆＊＠＝ＡＢＣＤＥＦＧＨＩＪＫＬＭＮＯＰＱＲＳＴＵＶＷＸＹＺａｂｃｄｅｆｇｈｉｊｋｌｍｎｏｐｑｒｓｔｕｖｗｘｙｚ/
y@\/,.･:;?!'^~_(){}[]@￥／，．・：；？！’＾￣＿（）｛｝［］@
}

